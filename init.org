#+PROPERTY: header-args:emacs-lisp :comments link :tangle yes
#+STARTUP: content
#+TITLE:  Emacs Initialization
#+AUTHOR: Perry Smith
#+EMAIL:  pedz@easesoftware.com

* Set up key external packages

We need to set up our package manager and our package configuration
macros early so they are available for the rest of the init process.

** Maybe set debug-on-error

We can tangle enable / disable this when we need to debug our init
code.

#+begin_src emacs-lisp :comments no :tangle no
  ;;; init.el --- init  -*- no-byte-compile: t -*-
  (setq debug-on-error t)
#+end_src

** macOS path setup

Over the years, macOS has changed how to set environment variables for
programs launched by the Finder.  I eventually just gave up and
initialize ~$PATH~ and ~$MANPATH~ via Elisp code.

The proper way to configure ~$PATH~ and ~$MANPATH~ environment
variables on macOS is to put files into ~/etc/paths.d~ and
~/etc/manpaths.d~.  Then use ~/usr/libexec/path_helper~ to spew out
the configured environment variables.

Part of the code checks to see if ~$USER~ is in the ~$PATH~ somewhere
already.  The theory is that I add in ~$HOME/bin~ into ~$PATH~ in my
shell login scripts so if I find ~$USER~, I assume Emacs is being
started from the command prompt and not by the Finder.

I also prepend ~~/bin~ to ~$PATH~.  I currently don't do this for
non-macOS systems.

Sample output of what I am attempting to parse:
#+begin_example
setenv PATH "/opt/homebrew/bin:/opt/homebrew/sbin:/usr/local/bin:/usr/bin:/bin:/usr/sbin:/sbin:/usr/local/MacGPG2/bin:/opt/X11/bin";
setenv MANPATH "/usr/share/man:/usr/local/share/man:/usr/local/MacGPG2/share/man:/opt/homebrew/share/man:/opt/X11/share/man";
#+end_example

#+begin_src emacs-lisp
  (if (eq system-type 'darwin)
      (let* ((path-helper "/usr/libexec/path_helper")
	     path-helper-output)
	(unless (or (string-match (getenv "USER") (getenv "PATH"))
		    (not (file-executable-p path-helper)))
	  (setenv "PATH" "")                  ; For consistency, zap PATH back to an empty string
	  (setenv "MANPATH" "")               ; ditto for MANPATH
	  (setq path-helper-output (shell-command-to-string (concat path-helper " -c")))
	  (string-match "setenv \\([A-Z]+\\) \"\\([^\"]+\\)\";\nsetenv \\([A-Z]+\\) \"\\([^\"]+\\)\":?;\n"
			path-helper-output)
	  (setenv (match-string 1 path-helper-output) (match-string 2 path-helper-output))
	  (setenv (match-string 3 path-helper-output) (match-string 4 path-helper-output))
	  (setenv "PATH" (concat (expand-file-name "~/bin") ":" (getenv "PATH"))))))
#+end_src
** Modify exec-path

[[elisp:(describe-variable 'exec-path)][exec-path]] starts with a list of directories which could include
directories not in PATH.  A specific example are the directories that
contain executables within the Emacs.app folder on macOS.

This code takes each directory in PATH and adds it to the exec-path
list.  The order will be munged but so far that hasn't bit me.

#+begin_src emacs-lisp
  (dolist (dir (split-string (getenv "PATH") ":"))
    (add-to-list 'exec-path dir))
#+end_src
** Set up Borg

[[https://melpa.org/#/borg][Borg]] is a replacement to ~package.el~ or ~el-get~.  The idea is that
you pull down git repos and use them which enables making changes and
pull requests easier.  This is not much different from ~el-get~ except
instead of fixed recipes it leaves you to decide to use ~use-package~
or [[https://elpa.gnu.org/packages/leaf.html][leaf]].

#+begin_src emacs-lisp
  (when (< emacs-major-version 27)
    (setq package-enable-at-startup nil))
  (eval-and-compile
    (add-to-list 'load-path (expand-file-name "lib/borg" user-emacs-directory)))
  (require 'borg)
  (borg-initialize)
#+end_src

** Set up Leaf

I picked [[https://elpa.gnu.org/packages/leaf.html][leaf]] because it understood el-get but now it seems that is
mute because I stopped using ~el-get~.

#+begin_src emacs-lisp
  (eval-when-compile
    (add-to-list 'load-path (expand-file-name "lib/leaf" user-emacs-directory))
    (require 'leaf))
  (leaf leaf-keywords
    :defun leaf-keywords-init
    :config
    (leaf-keywords-init))
#+end_src

** Set up Magit

[[https://melpa.org/#/magit][magit]] is "A Git porcelain inside Emacs."  The ~defer-config~ is what
is recommended by ~borg~ so the submodules are visible.

#+begin_src emacs-lisp
  (leaf magit
    :defun magit-add-section-hook
    :defer-config
    (magit-add-section-hook 'magit-status-sections-hook
			    'magit-insert-modules
			    'magit-insert-stashes
			    'append))
#+end_src

* Original init file

** Set up code to help debugging at the front

#+begin_src emacs-lisp
  ;; Nifty for debugging load issues and sequences
  ;; This puts out a Loading message all the time
  ;; (setq force-load-messages t)
  ;; (defadvice require (before load-log activate)
  ;;   (message "Requiring %s" (ad-get-arg 0)))
#+end_src

** Customize zsh should it be used within an inferior shell

There are no explicit references to [[elisp:(describe-variable 'explicit-zsh-args)][explicit-zsh-args]].  It is used
implicitly by [[elisp:(describe-function 'shell)][shell]] when the interactive shell is `zsh'.  This code
tells zsh to not use ZLE and that the shell is interactive.  I'm not
sure why I went to the trouble of making it customizable but I did.

#+begin_src emacs-lisp
  (defcustom explicit-zsh-args '("+Z" "-i")
    "Args passed to inferior shell by \\[shell], if the shell is zsh.
    Value is a list of strings, which may be nil."
    :type '(repeat (string :tag "Argument"))
    :group 'shell)
#+end_src

** Pull in customized variables

The reason to set [[elisp:(describe-variable 'custom-file)][custom-file]] is because when a variable is written,
this is the file that Emacs goes out and modifies.

#+begin_src emacs-lisp
  (setq custom-file (expand-file-name "customize.el" user-emacs-directory))
  (load custom-file)
#+end_src

** Pull in ~pedz~

Currently I have things split up into separate files all loaded by
~pedz.el~.  This may change as I work on this set up.

#+begin_src emacs-lisp
  (eval-and-compile
    (add-to-list 'load-path (expand-file-name "pedz" user-emacs-directory)))
  ;; See https://github.com/emacscollective/borg/discussions/135#discussioncomment-3954786
  ;; (declare-function pedz/org-require "early-init" (sym &optional dont-load paths))
  (eval-when-compile
    (defun pedz/org-require (sym &optional dont-load paths)))
  (pedz/org-require 'pedz)
#+end_src

** Set up creating Org mode code blocks easily.

#+begin_src emacs-lisp
  (pedz/org-require 'org-code-blocks)
#+end_src
